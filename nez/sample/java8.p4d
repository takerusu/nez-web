// PEG4d Java8 Grammar
// ===============

export
	= Statement
	/ Expression
	/ Constant

File
	= _ { ( @TopLevel / _ )* #Source } EOT

Chunk
	= TopLevel
	/ Statement
	/ Expression
	/ Type
_
	= (S / BLOCKCOMMENT / LINECOMMENT )*

S
	= [ \n\r\t\u000B\u000C]

BLOCKCOMMENT
	= '/*' (!'*/' .)* '*/'

LINECOMMENT
	= '//' (!'\n' .)*


EOL
	= ('\r'? '\n' / !.)

DIGIT
	= [0-9]

W
	= [a-zA-Z0-9_$]
	/ UCHAR

UCHAR
	= '\\u' HEX4
	/ '\\U' HEX4 HEX4

HEX4
	= HEX HEX HEX HEX

Name
	= { NAME #Name } _

NAME
	= !DIGIT !(KEYWORD !W) W+

KEYWORD
	= 'abstract' / 'assert' / 'boolean' / 'break' / 'byte' / 'case'
	/ 'catch' / 'char' / 'class' / 'const' / 'continue' / 'default'
	/ 'do' / 'double' / 'else' / 'enum' / 'extends' / 'false'
	/ 'final' / 'finally' / 'float' / 'for' / 'goto' / 'if' / 'implements'
	/ 'import' / 'instanceof' / 'int' / 'interface' / 'long' / 'native'
	/ 'new' / 'null' / 'package' / 'private' / 'protected' / 'public'
	/ 'return' / 'short' / 'static' / 'strictfp' / 'super' / 'switch'
	/ 'synchronized' / 'this' / 'throw' / 'throws' / 'transient'
	/ 'true' / 'try' / 'void' / 'volatile' / 'while'

"++"     = '++' _
"--"     = '--' _
"&"      = '&' ![&=] _
"*"      = '*' !'=' _
"+"      = '+' ![+=] _
"-"      = '-' !('-' / [=>]) _
"~"      = '~' _
"!"      = '!' !'=' _

"{"      = '{' _
"["      = '[' _
"]"      = ']' _
","      = ',' _
"..."    = '...' _
":"      = ':' ![>] _
"/"      = '/' !'=' _
"%"      = '%' ![=>] _
"<<"     = '<<' !'=' _
">>"     = '>>' ![=>] _
">>>"    = '>>>' !'=' _
"<="     = '<=' _
">="     = '>=' _
"<"      = '<' ![=] _
">"      = '>' ![=] _
"=="     = '==' _
"!="     = '!=' _
"^"      = '^' ![=] _
"|"      = '|' ![|=] _
"&&"     = '&&' _
"||"     = '||' _
"?"      = '?' _
";"      = (';' _ / EOL)  /* this is for debug */
"}"      = '}' _

"*="     = '*=' _
"/="     = '/=' _
"%="     = '%=' _
"+="     = '+=' _
"-="     = '-=' _
"<<="    = '<<=' _
">>="    = '>>=' _
">>>="   = '>>>=' _
"&="     = '&=' _
"^="     = '^=' _
"|="     = '|=' _
"="      = '=' !'=' _
"."      = '.' _

"("      = '(' _
")"      = ')' _

"@"      = '@' _
"->"     = '->' _

// reserved keyword
"abstract"          = 'abstract'          !W _
"assert"            = 'assert'            !W _
"boolean"           = 'boolean'           !W _
"break"             = 'break'             !W _
"byte"              = 'byte'              !W _
"case"              = 'case'              !W _
"catch"             = 'catch'             !W _
"char"              = 'char'              !W _
"class"             = 'class'             !W _
"const"             = 'const'             !W _
"continue"          = 'continue'          !W _
"default"           = 'default'           !W _
"do"                = 'do'                !W _
"double"            = 'double'            !W _
"else"              = 'else'              !W _
"enum"              = 'enum'              !W _
"extends"           = 'extends'           !W _
"false"             = 'false'             !W _
"final"             = 'final'             !W _
"finally"           = 'finally'           !W _
"float"             = 'float'             !W _
"for"               = 'for'               !W _
"goto"              = 'goto'              !W _
"if"                = 'if'                !W _
"implements"        = 'implements'        !W _
"import"            = 'import'            !W _
"instanceof"        = 'instanceof'        !W _
"int"               = 'int'               !W _
"interface"         = 'interface'         !W _
"long"              = 'long'              !W _
"native"            = 'native'            !W _
"new"               = 'new'               !W _
"null"              = 'null'              !W _
"package"           = 'package'           !W _
"private"           = 'private'           !W _
"protected"         = 'protected'         !W _
"public"            = 'public'            !W _
"return"            = 'return'            !W _
"short"             = 'short'             !W _
"static"            = 'static'            !W _
"strictfp"          = 'strictfp'          !W _
"super"             = 'super'             !W _
"switch"            = 'switch'            !W _
"synchronized"      = 'synchronized'      !W _
"this"              = 'this'              !W _
"throw"             = 'throw'             !W _
"throws"            = 'throws'            !W _
"transient"         = 'transient'         !W _
"true"              = 'true'              !W _
"try"               = 'try'               !W _
"void"              = 'void'              !W _
"volatile"          = 'volatile'          !W _
"while"             = 'while'             !W _


EOT = !.

TopLevel
	= PackageDeclaration
	/ ImportDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration


// package definition
PackageDeclaration
	= { (@PackageModifier)* _ "package" _ @TypeName ";" #PackageDeclaration }

PackageModifier
	= Annotation

// import definition
ImportDeclaration
	= TypeImportOnDemandDeclaration
	/ SingleTypeImportDeclaration
	/ StaticImportOnDemandDeclaration
	/ SingleStaticImportDeclaration

SingleTypeImportDeclaration
	= "import" { @TypeName #SingleTypeImportDeclaration } _ ";"

TypeImportOnDemandDeclaration
	= "import" { @TypeName  _ "." "*" #TypeImportOnDemandDeclaration } ";"

SingleStaticImportDeclaration
	= "import" "static" { @TypeName _ '.' _ @Name #SingleStaticImportDeclaration } _ ";"

StaticImportOnDemandDeclaration
	= "import" "static" { @TypeName #StaticImportOnDemandDeclaration } _ "." "*" ";"

// annotation definition
Annotation
	= NormalAnnotation
	/ SingleElementAnnotation
	/ MarkerAnnotation

NormalAnnotation
	= "@" { @TypeName _ "(" (@ElementValuePairList)? _ ")" #NormalAnnotation }

MarkerAnnotation
	= "@" { @TypeName #MarkerAnnotation }

SingleElementAnnotation
	= "@" { @TypeName _ "(" @ElementValue _ ")" #SingleElementAnnotation }

ElementValuePairList
	= { @ElementValuePair ( _ "," @ElementValuePair)* #ElementValuePairList }

ElementValuePair
	= { @Name _ "=" @ElementValue #ElementValuePair }

ElementValue
	= ElementValueArrayInitializer
	/ ConditionalExpression
	/ Annotation

ElementValueArrayInitializer
	= "{" { (@ElementValueList)? #ElementValueArrayInitializer } _ ","? "}"

ElementValueList
	= { @ElementValue ( _ "," @ElementValue)* #ElementValueList}


// Declaration
//==============

//local variable
VariableDeclaration
	= { (@VariableModifier)* @Type @InitDecl ("," @InitDecl)* #Declaration }

VariableModifier
	= Annotation
	/ "final"     _ { #KeyValue @{`abstraction` #Key } @{`final` #Value } } _

InitDecl
	= { @VarName ("=" @Initializer)? #VarDecl } _

InitDeclAssign
	= { @VarName "=" @Initializer #VarDecl } _

VarName
	= Name {@ "[" (@ConstantExpression)? "]" #ArrayName}*

Initializer
	= Expression
	/ "{" { (addInitializerList)* ","? #Initializer } "}"

addInitializerList
	= (@Designation / @Initializer)? ("," (@Designation / @Initializer))*

Designation
	= { (@Designator)+ "=" @Initializer  #Designation }

Designator
	= "[" ConstantExpression "]"
	/ "." Name _

//class type, enum type
ClassDeclaration
	= { (@ClassModifier)* "class" @Name (@TGenerics)? (@SuperClass)? (@SuperInterfaces)* @ClassBody #ClassDeclaration } _
	/ { (@ClassModifier)* "enum" @Name (@SuperInterfaces)* @EnumBody #EnumDeclaration } _

ClassModifier
	= Annotation
	/ "public"    _ { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "protected" _ { #KeyValue @{`visibility` #Key } @{`protected` #Value } } _
	/ "private"   _ { #KeyValue @{`visibility` #Key } @{`private` #Value } } _
	/ "abstract"  _ { #KeyValue @{`abstraction` #Key } @{`abstract` #Value } } _
	/ "final"     _ { #KeyValue @{`abstraction` #Key } @{`final` #Value } } _
	/ "static"    _ { #KeyValue @{`static` #Key } @{`true` #Value } } _
	/ "strictfp"  _ { #KeyValue @{`strictfp` #Key } @{`true` #Value } } _

SuperClass
	= { "extends" _ @Type #SuperClass } _

SuperInterfaces
	= { "implements" _ @TypeList #SuperInterfaces } _

EnumBody
	= "{" { (@EnumeratorList)? ","? ";" (@ClassBodyDeclaration)+ #EnumBody } _ "}"
	/ "{" { (@EnumeratorList)? ","? ";"? #EnumBody } _ "}"

EnumeratorList
	= { @Enumerator ("," @Enumerator)* #List }

Enumerator
	= { (@Annotation)* @Name (@{ "(" addArgumentExpressionList? ")" #List })? (@ClassBody)? #Enumerator }

ClassBody
	= "{" { (@ClassBodyDeclaration)* #ClassBody } "}"

ClassBodyDeclaration
	= ClassMemberDeclaration
	/ { @Block #InstanceInisializer } _
	/ 'static' _ { @Block #StaticInitializer } _
	/ ConstructorDeclaration

ClassMemberDeclaration
	= FieldDeclaration
	/ MethodDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration

//interface, annotation type
InterfaceDeclaration
	= { (@InterfaceModifier)* "interface" @Name (@TGenerics)? (@ExtendsInterfaces)* @InterfaceBody #InterfaceDeclaration } _
	/ { (@InterfaceModifier)* '@' "interface" @Name @AnnotationTypeBody #AnnotationTypeDeclaration } _

InterfaceModifier
	= Annotation
	/ "public"    _ { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "protected" _ { #KeyValue @{`visibility` #Key } @{`protected` #Value } } _
	/ "private"   _ { #KeyValue @{`visibility` #Key } @{`private` #Value } } _
	/ "abstract"  _ { #KeyValue @{`abstraction` #Key } @{`abstract` #Value } } _
	/ "static"    _ { #KeyValue @{`static` #Key } @{`true` #Value } } _
	/ "strictfp"  _ { #KeyValue @{`strictfp` #Key } @{`true` #Value } } _

ExtendsInterfaces
	= { "extends" _ @TypeList #ExtendsInterfaces } _

InterfaceBody
	= "{" { (@InterfaceMemberDeclaration)* #InterfaceBody } "}"

InterfaceMemberDeclaration
	= ConstantDeclaration
	/ InterfaceMethodDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration

AnnotationTypeBody
	= "{" { (@AnnotationTypeMemberDeclaration)* #AnnotationTypeBody } "}"

AnnotationTypeMemberDeclaration
	= AnnotationTypeElementDeclaration
	/ ConstantDeclaration
	/ ClassDeclaration
	/ InterfaceDeclaration

AnnotationTypeElementDeclaration
	= { (@AnnotationTypeElementModifier)* @Type @Name "(" ")" (@AnnotationTypeElementDefaultValue)? ";" #AnnotationTypeElement } _

AnnotationTypeElementModifier
	= Annotation
	/ "public"    _ { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "abstract"  _ { #KeyValue @{`abstraction` #Key } @{`abstract` #Value } } _

AnnotationTypeElementDefaultValue
	= "default" ElementValue


//field, constant
FieldDeclaration
	= { (@FieldModifier)* @Type @InitDecl ("," @InitDecl)*  ";" #Field } _

FieldModifier
	= Annotation
	/ "public"    { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "protected" { #KeyValue @{`visibility` #Key } @{`protected` #Value } } _
	/ "private"   { #KeyValue @{`visibility` #Key } @{`private` #Value } } _
	/ "final"     { #KeyValue @{`final` #Key } @{`true` #Value } } _
	/ "static"    { #KeyValue @{`static` #Key } @{`true` #Value } } _
	/ "transient" { #KeyValue @{`transient` #Key } @{`true` #Value } } _
	/ "volatile"  { #KeyValue @{`volatile` #Key } @{`true` #Value } } _

ConstantDeclaration
	= { (@ConstantModifier)* @Type @InitDecl ("," @InitDecl)*  ";" #Constant } _

ConstantModifier
	= Annotation
	/ "public"    { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "final"     { #KeyValue @{`final` #Key } @{`true` #Value } } _
	/ "static"    { #KeyValue @{`static` #Key } @{`true` #Value } } _

//method
MethodDeclaration
	= { (@MethodModifier)* (@TGenerics (@Annotation)*)? @Result @Name "(" @MethodParamList ")" (@Throws)? (@Block / ";") #Method } _

MethodModifier
	= Annotation
	/ "public"        { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "protected"     { #KeyValue @{`visibility` #Key } @{`protected` #Value } } _
	/ "private"       { #KeyValue @{`visibility` #Key } @{`private` #Value } } _
	/ "abstract"      { #KeyValue @{`abstraction` #Key } @{`abstract` #Value } } _
	/ "final"         { #KeyValue @{`abstraction` #Key } @{`final` #Value } } _
	/ "static"        { #KeyValue @{`static` #Key } @{`true` #Value } } _
	/ "synchronized"  { #KeyValue @{`synchronized` #Key } @{`true` #Value } } _
	/ "native"        { #KeyValue @{`native` #Key } @{`true` #Value } } _
	/ "strictfp"      { #KeyValue @{`strictfp` #Key } @{`true` #Value } } _

InterfaceMethodDeclaration
	= { (@InterfaceMethodModifier)* (@TGenerics (@Annotation)*)? @Result @Name "(" @MethodParamList ")" (@Throws)? (@Block / ";") #Method } _

InterfaceMethodModifier
	= Annotation
	/ "public"        { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "abstract"      { #KeyValue @{`abstraction` #Key } @{`abstract` #Value } } _
	/ "default"       { #KeyValue @{`abstraction` #Key } @{`default` #Value } } _
	/ "static"        { #KeyValue @{`static` #Key } @{`true` #Value } } _
	/ "strictfp"      { #KeyValue @{`strictfp` #Key } @{`true` #Value } } _

Result
	= { "void" #TVoid } _
	/ Type

MethodParamList
	= { (@MethodParam ",")* @VarArgs #List }
	/ { ( @MethodParam ("," @MethodParam)* )? #List }

MethodParam
	= { (@VariableModifier)* @Type _ @VarName #Param } _

VarArgs
	= { @Type _ "..." @VarName #VarArgs }

Throws
	= "throws" { @TypeList #Throws } _

//constructor
ConstructorDeclaration
	= { (@ConstructorModifier)* @Type "(" @MethodParamList ")" (@Throws)? @ConstructorBody #Constructor } _

ConstructorModifier
	= Annotation
	/ "public"        { #KeyValue @{`visibility` #Key } @{`public` #Value } } _
	/ "protected"     { #KeyValue @{`visibility` #Key } @{`protected` #Value } } _
	/ "private"       { #KeyValue @{`visibility` #Key } @{`private` #Value } } _

ConstructorBody
	= { "{" (@ExplicitConstructorInvocation)? (@Statement / @LocalDeclaration)* "}" #Block }

ExplicitConstructorInvocation
	= { (@TGenerics)? @{ "this"  "(" addArgumentExpressionList? ")" #List } #ExplicitConstructorInvocation  } ";"
	/ { (@TGenerics)? @{ "super" "(" addArgumentExpressionList? ")" #List } #ExplicitConstructorInvocation  } ";"
//	/ { @PrimaryExpression '.' (@TGenerics)? "super" @{ "(" addArgumentExpressionList? ")" #List } #ExplicitConstructorInvocation  } ";"
//	/ { @TypeName '.' (@TGenerics)? "super" @{ "(" addArgumentExpressionList? ")" #List } #ExplicitConstructorInvocation  } ";"


//Type
//====

Type
	= (PrimaryType / ReferenceType) {@ "[" _? "]" #TArray }*

TypeList
	= { @TypeElement ("," @TypeElement)* #TypeList }

TypeElement
	= (Type / { "?" #TWildCard }) {@ (@{ _ "extends" @Type ("&" @Type)* #SuperType })? #TypeElement }

TGenerics
	= { "<" @TypeList ">" #Generics } _
	/ { "<" ">" #Diamond } _

TypeName
	= Name {@ _ "." @Name #TypeName }*

ReferenceType
	= { @TypeName @TGenerics #TGenerics } _
	/ TypeName _

PrimaryType
	= { "byte"         #TInt } _
	/ { "char"         #TInt } _
	/ { "short"        #TInt } _
	/ { "int"          #TInt } _
	/ { "long"         #TInt } _
	/ { "float"        #TFloat } _
	/ { "double"       #TFloat } _
	/ { "boolean"      #TBoolean } _


// Block, Statement
//===================

Block
	= { "{" (@Statement / @LocalDeclaration)* "}" #Block }

LocalDeclaration
	= VariableDeclaration ";"
	/ ClassDeclaration

Statement
	= Block
	/ { "assert" @Expression (":" @Expression)? ";" #Assert }
	/ { "if"   "(" @Expression ")" @Statement ("else" @Statement)? #If }
	/ { "switch" "(" @Expression ")" @Statement #Switch }
	/ { "while"  "(" @Expression ")" @Statement #While }
	/ { "do" @Statement "while" "(" @Expression ")" ";" #DoWhile }
	/ { "for" "(" (@Expressions)?      ";" (@Expression)? ";" (@Expressions)? ")" @Statement #For }
	/ { "for" "(" @VariableDeclaration ";" (@Expression)? ";" (@Expressions)? ")" @Statement #For }
	/ { "for" "(" (@VariableModifier)* @Type @Name ":" @Expression ")" @Statement #ForEach }
	/ { "goto" @Name ";" #Goto }
	/ { "continue" ";" #Continue }
	/ { "break" ";" #Break }
	/ { "return" (@Expression)?  ";" #Return }
	/ { "try" @ResourceStatement @Block (@CatchBlock)* (@{ "finally" @Block #Finally })? #Try }
	/ { "try" @Block (@CatchBlock)* @{ "finally" @Block #Finally } #Try }
	/ { "try" @Block (@CatchBlock)+ #Try }
	/ { "throw" @Expression ";" #Throw }
	/ { "case" @ConstantExpression ":" (!"case" !"default" @Statement)* #SwitchCase }
	/ { "default" ":" (!"case" @Statement)* #SwitchDefault }
	/ { @Name ":" #Label }
	/ { @Expression ";" #ExpressionStatement }
	/ { #Empty } ";"

//trycatch
ResourceStatement
	= "(" { @Resource _ (";" @Resource _ )* #ResourceList } ";"? ")"

Resource
	= { (@VariableModifier)* @Type @Name "=" @Expression #Declaration }

CatchBlock
	= { "catch" "(" @CatchParameter ")" @Block #Catch }

CatchParameter
	= { (@VariableModifier)* @Type @Name ("|" @Name)* #Declaration }


//Expression
//==========

Expression
	= LambdaExpression
	/ AssignmentExpression

Expressions
	= { @Expression ("," @Expression)* #Expressions }

AssignmentExpression
	= { @UnaryExpression addAssignmentOperator @Expression }
	/ ConditionalExpression

addAssignmentOperator
	= "="    #Assign
	/ "*="   #AssignMul
	/ "/="   #AssignDiv
	/ "%="   #AssignMod
	/ "+="   #AssignAdd
	/ "-="   #AssignSub
	/ "<<="  #AssignLeftShift
	/ ">>="  #AssignArithmeticRightShift
	/ ">>>=" #AssignLogicalRightShift
	/ "&="   #AssignBitwiseAnd
	/ "^="   #AssignBitwiseXOr
	/ "|="   #AssignBitwiseOr

ConstantExpression
	= ConditionalExpression

ConditionalExpression
	= LogicalORExpression {@ "?"  @Expression ":" @LogicalORExpression #Trinary }*

LogicalORExpression
	= LogicalANDExpression {@ "||" @LogicalANDExpression #Or }*

LogicalANDExpression
	= InclusiveORExpression {@ "&&" @InclusiveORExpression #And }*

InclusiveORExpression
	= ExclusiveORExpression {@ "|" @ExclusiveORExpression #BitwiseOr }*

ExclusiveORExpression
	= ANDExpression {@ "^" @ANDExpression #BitwiseXor }*

ANDExpression
	= EqualityExpression {@ "&" @EqualityExpression #BitwiseAnd }*

EqualityExpression
	= RelationalExpression {@ ( "==" #Equals / "!=" #NotEquals ) @RelationalExpression }*

relationalOperator
	= "<=" #LessThanEquals
	/ ">=" #GreaterThanEquals
	/ "<" #LessThan
	/ ">" #GreaterThan

RelationalExpression
	= ShiftExpression {@ ( relationalOperator @ShiftExpression / "instanceof" @Type #InstanceOf ) }*

ShiftExpression
	= AdditiveExpression {@ ( "<<" #LeftShift / ">>" #ArithmeticRightShift / ">>>" #LogicalRightShift ) @AdditiveExpression }*

AdditiveExpression
	= MultiplicativeExpression {@ ( "+" #Add / "-" #Sub ) @MultiplicativeExpression }*

MultiplicativeExpression
	= CastNewExpression {@ ( "*" #Mul / "/" #Div / "%" #Mod ) @CastNewExpression }*

CastNewExpression
	= { "(" @Type ")" @CastNewExpression #Cast }
	/ UnaryExpression

UnaryExpression
	= PostfixExpression
	/ { "++" @UnaryExpression #PrefixInc }
	/ { "--" @UnaryExpression #PrefixDec }
	/ { "+"  @CastNewExpression #Plus }
	/ { "-"  @CastNewExpression #Minus }
	/ { "~"  @CastNewExpression #Compl }
	/ { "!"  @CastNewExpression #Not }

PostfixExpression
	= (PrimaryExpression / InstanceCreationExpression / ArrayCreationExpression)
		{@ (addFunctionCall / addIndex / addField / addMethodReference / addInc / addDec) }*

InstanceCreationExpression
	= { "new" (@TGenerics)? (@Annotation)* @Type @{ "(" addArgumentExpressionList? ")" #List } (@ClassBody)? #InstanceCreation }

ArrayCreationExpression
	= { "new" (@Annotation)* @(Type {@ (@Annotation)* "[" @Expression "]" #ArraySize }+) #ArrayCreation }

addFunctionCall
	= @{ "(" addArgumentExpressionList? ")" #List} #Apply

addArgumentExpressionList
	= (@Expression ("," @Expression)*)

addIndex
	= "[" @Expression "]" #Index

addField
	= "." @Name #Field

addMethodReference
	= "::" @(Name / { "new" #New }) #MethodReference

addInc
	= "++" #Inc

addDec
	= "--" #Dec

PrimaryExpression
	= Constant
	/ "(" Expression ")"
	/ { "this" #This }
	/ { "super" #Super }
	/ { @Type "." "class" #Class }
	/ Name

LambdaExpression
	= { @LambdaParameters _ "->" @LambdaBody #LambdaExpression }

LambdaParameters
	= { (@Name
		/ @{ "(" addArgumentExpressionList? _ ")" #AddArgumentExpressionList }
		/ "(" @InferredFormalParameterList _ ")"
		) #LambdaParameters }

InferredFormalParameterList
	= { @Name ( _ "," @Name)* #InferredFormalParameterList }

LambdaBody
	= { (@Expression / @Block) #LambdaBody }


//Literal
//=======

Constant
	= JavaFloat
	/ JavaInteger
	/ JavaBoolean
	/ JavaString
	/ JavaChar
	/ JavaNull

JavaFloat
	= { FLOAT #Float } FLOAT_SUFFIX? _

FLOAT
	= FRACTION EXPONENT?
	/ DIGIT+ EXPONENT

FRACTION
	= !'_' ('_'* DIGIT)* '.' DIGIT ('_'* DIGIT)*
	/ DIGIT ('_'* DIGIT)* '.'

EXPONENT
	= [eE] [+\-]? DIGIT ('_'* DIGIT)*

FLOAT_SUFFIX
	= [fFdD]

JavaInteger
	= { ( DECIMAL / HEXADECIMAL ) #Integer } INT_SUFFIX? _
//	= { ( BINARY / DECIMAL / HEXADECIMAL / OCTAL ) #Integer } INT_SUFFIX? _

BINARY
	= BIN_PREFIX [01] ('_'* [01])*

BIN_PREFIX
	= '0' [bB]

DECIMAL
	= '0' ![0-9xXbB]
	/ [1-9] ('_'* DIGIT)*

HEXADECIMAL
	= HEX_PREFIX HEX ('_'* HEX)*

HEX_PREFIX
	= '0' [xX]

OCTAL
	= '0' ('_'* [0-7])*

INT_SUFFIX
	= [lL]

JavaBoolean
	= { "true"  #True } _
	/ { "false" #False } _

JavaString
	= '"' { STRING_CONTENT* #String } '"' _

JavaChar
	= "'" { CHAR_CONTENT* #Character } "'" _

STRING_CONTENT
	= ESCAPE
	/ ![\"\n\\] .

CHAR_CONTENT
	= ESCAPE
	/ !['\n\\] .

ESCAPE
	= SIMPLE_ESCAPE
	/ OCTAL_ESCAPE
	/ HEX_ESCAPE
	/ UCHAR

SIMPLE_ESCAPE
	= '\\' ['\"\\bfnrt]

OCTAL_ESCAPE
	= '\\' [0-3] [0-7]? [0-7]?

HEX_ESCAPE
	= '\\' 'u'+ HEX HEX HEX HEX

JavaNull
	= { 'null' #Null } _
